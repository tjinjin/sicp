## 1.1
### 1.1.1 式
前置記法の話

```
> (* 25 4 12)
1200
> (+ (* 3 5)(- 10 6))
```

プリティプリントという記法。人がわかりやすいように演算子を縦にそろえて書く

### 1.1.2 命名と環境
`define` を使って変数を定義する？

```
> (define pi 3.141419)
> (define radius 10)
> (* pi (* radius radius))
314.159
```

### 1.1.3 組み合わせの評価
組み合わせの評価は主に2ステップ

- 組み合わせの部分式を評価
- 部分式の左にある演算子の手続きを、部分式に適用する

これらは本質的に再帰的な処理。

```
(* (+ 2 (* 4 6))
   (+ 3 5 7))
```

これをツリーで表すとよい。終端ノードから上に向かって伝わっていく様子は木の集積（tree accumulation）と呼ばれる

組み合わせの部分式を評価していくとあるタイミングで、数値や演算子、その他の名前というような基本式になる。これらを下記のように扱っている

- 数字の値は値として扱う
- 組み込み演算子は機械語の列で、それに対応する操作を行う
- その他の名前は、現在の環境で名付けられたオブジェクトとみなす

組み込み演算子はある意味オブジェクトとみなすこともできる。

上の3ステップは定義については扱っていない。例えば`(define x 3)`はあくまで記号xと値を関連付けること。

### 1.1.4 複合手続きモデル

```
(define (square x) (* x x))
```

複合演算に名前を付けたもの。複合手続きと呼ぶ。

```
(define (<名前> <仮引数>)
  <本体>)
```

のような形式になる

重ねることもできる


```
(define (sum-of-squares x y)
  (x (square x) (square y)))
(sum-of-squares 3 4)
25
```

```
(define (f a)
  (sum-of-squares (+ a 1)(* a 2)))
(f 5)
136
```

複合手続きは基本手続きとまったく同じように使うことができ、見分けることはできない

### 1.1.5 手続き適用の置換モデル
複合手続きでインタプリタは組み合わせの各要素を評価し、手続きを引数に適用する流れになる

  複合手続きを引数に適用するには、手続きの本体に出てくる仮引数を対応する引数で置き換えてそれを評価する

ex> (f 5)

まず本体を取得

(sum-of-squares (+ a 1) (* a 2))

次に仮引数を置き換える

(sum-of-squares (+ 5 1) (* 5 2))

手続きを実行できるように演算子を計算して手続きの形にする

(+ (square 6) (square 10))

これをまた置き換える

(+ (* 6 6) (* 10 10))
(+ 36 100)
136

この手順は置換モデル（substitution model）と呼ばれる。この段階ではあくまでモデルで、これがインタプリタの動きではないので注意

```
評価手順について形式的に考えるための最初の一歩となるものに過ぎません。一般に、科学や工学で現象をモデル化するときには、単純化した不完全なモデルから始めます。物事を詳細に調べていくにつれ、これらの単純なモデルは不適切になり、より精密なモデルで置き換えられな帰ればならない。
```

単純化するってことは要素を削ぎ落とすことだもんね。

遅延？モデル（値が必要になるまで非演算子を計算しない

```
(sum-of-squares (+ 5 1) (* 5 2))
(+ (square (+ 5 1)) (square (* 5 2)) )
(+ (* (+ 5 1) (+ 5 1)) (* (* 5 2) (* 5 2)) )
(+         (* 6 6)            (* 10 10))
(+ 36 100)
136
```

後者の完全に展開してから簡約するというものは*正規順序評価* (normal-order evaluation)と呼ばれ、インタプリタが実際に使っている引数を評価してから適用するという方法は*適用順序評価*(applicative-order evaluation)と呼ばれる。置換によってモデル化できる場合は正規順序評価と適用順序評価は同じ値になる。
Lispでは適用順序評価を使っている。

### 1.1.6 条件式と述語

場合分け（case analysis）

```
(define (abs x)
  (cond ((> x 0) x)
        ((= x 0) 0)
        ((< x 0) (- x))))
```

一般式では下記の形式

```
(cond (<p1> <e1>)
      (<p2> <e2>)
      ...
      (<pn> <en>))
```

記号condに続く節（clause）という括弧のペアの列によって構成される。
ペアの最初の式は述語（predicate）―値が真偽で解釈される式

`<p1>`から解釈され、偽なら`<p2>`へと真が見つかるまで続く。見つかると結果式`<e>`を返す
述語は手続きに対しても利用できる`(< x 0)`など

以下のようにも書ける

```
(define (abs x)
  (cond ((< x 0) (- x))
        (else x)))
```

ifだと下記のようになる

```
(define (abs x)
  (if (< x 0)
       (- x)
       x))
```

ifは場合分けが2つの場合からなる場合に使える。

```
(if <predicate> <consequent> <alternative>)
```

#### 論理複合演算
- (and <e1> ... <en>)
  - 左から右に一つずつ評価する。どれか一つが偽と評価されると全体として偽になり、残りの<e>は評価されない
- (or <e1> ... <en>)
  - 左から右に一つずつ評価する。<e>のどれか一つが真と評価されると、全体として真になり、残りは評価されない
- (not <e>)
  - 式が真なら偽に、偽なら真になる

andとorは特殊形式で手続きではない（最後まで評価されないから）。notは手続き

```
(define (>= x y) (or (> x y) (= x y)))
(define (>= x y) (not (< x y)))
```

### 1.1.7 例：ニュートン方による平方根
数学関数と手続きは別物。数学では宣言的な（何であるか）記述が関心の対象であるが、コンピュータ・サイエンスでは命令的な（どうやるか）記述が感心の対象である
宣言敵はこれはこういうものだという性質を表す感じで、命令的とはこれはこうやればできるよって感じかな

平方根の一般的な求め方はニュートンの逐次近似法を使うもので、数値xの平方根の推定値yがあるとき、yとx/yの平均を取るというもの。
平均の値を推定値としてサイド計算していくことで近似解の精度があがる

基本的な手順をLispで書くと下記の通り

```
(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x) x )))

(define (improve guess x)
    (average guess (/ x guess)))

(define (average x y)
    (/ (+ x y) 2))

(define (good-enough? guess x)
    (< (abs (- (square guess) x)) 0.001))

(define (sqrt x)
    (sqrt-iter 1.0 x)
    )

(define (square x) (* x x))

(sqrt 9)
3.00009155413138
```

### 1.1.8 ブラックボックス抽象化としての手続き
sqrtプログラムは手続きの塊でできている。module化することで中の処理をしらなくても外部から使うことができる。例えばgood-enough?はsquareがどうやって計算するのかには感心がなく、とにかく二乗を計算するという事実だけに興味がある。
good-enough?にとってsquareは手続きというより手続きを抽象化した手続き抽象（procedural abstraction）と考えられる。RubyでいうところのProcみたいなもん。

なので下記ような書き方もできるが手続きとしては区別不可能であるべき。

```
(define (square x) (* x x))
(define (square x) (exp (double (log x))))
(define (double x) (+ x x))
```

#### 局所化

また手続きを使う側が知らなくていいものに仮引数がある。仮引数の名前は手続き内で局所化されている。ローカル変数みたいなもんだと思う。
こういったものを束縛変数（bound variable）といい、手続き定義は仮引数を束縛（bind）するという言い方をする

good-enough?で`\<,-,abs,square`は自由変数であると言える。

#### 内部定義とブロック構造

```
(define (sqrt x)
    (sqrt-iter 1.0 x)
    )
(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x) x )))
(define (good-enough? guess x)
    (< (abs (- (square guess) x)) 0.001))
(define (improve guess x)
    (average guess (/ x guess)))
(define (average x y)
    (/ (+ x y) 2))
(define (square x) (* x x))
```

大きなプログラムを複数人で作る際に補助手続きとしてimproveなどを作ると他の人と被る可能性がある
なので、sqrtの中にサブ手続きを隠し、sqrtがほかの逐次近似法と共存できるようにしたい

```
(define (sqrt x)
  (define (good-enough? guess x)
      (< (abs (- (square guess) x)) 0.001))
  (define (improve guess x) (average guess (/ x guess)))
  (define  (sqrt-iter guess x)
    (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x) x )))
    (sqrt-iter 1.0 x))
```

ブロック構造（block structure）とよばれる書き方。ブロックで書くとスコープないで変数の共存ができる。
ここではxを内部定義内で自由変数にできる。こういった規定をレキシカルスコーピングと呼ぶ

```
(define (sqrt x)
  (define (good-enough? guess)
      (< (abs (- (square guess) x)) 0.001))
  (define (improve guess) (average guess (/ x guess)))
  (define  (sqrt-iter guess)
    (if (good-enough? guess)
      guess
      (sqrt-iter (improve guess))))
    (sqrt-iter 1.0))
```
